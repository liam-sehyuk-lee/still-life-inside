label floor_5:
    # 차가운 푸른 조명이 감도는 방. 중앙에 기이할 정도로 깨끗한 거울이 있고, 옆 탁자에는 약통이 있다.
    scene bg_5f_mental with dissolve

    "차가운 푸른빛이 감도는 방. 이전 층들의 비릿한 냄새 대신, 소독약 냄새 같은 것이 코를 찌른다."
    "방 중앙에는 기이할 정도로 깨끗한 거울이 놓여 있다."
    
    "나를 보는 건... 나인가?"
    "아니야, 이건... 나 같은 형체일 뿐."

    "거울 속 눈동자가 나를 따라오지 않는다."
    "내가 오른쪽으로 움직이면, 거울 속의 '나'는 왼쪽으로 움직인다. 내가 손을 들면, 저것은 비웃듯 입꼬리를 올린다."

    "약... 저 약을 먹으면"
    "이 눈에서 벗어날 수 있을까?"
    
    m "먹어. 그러면... 너도 우리와 같아질 수 있어."
    
    "익숙한 목소리. 나를 이곳에 가둔 '그것'의 목소리다."
    "하지만 이상하게도... 거부감이 들지 않는다. 저 목소리의 제안이 끔찍하게 달콤하게 들린다."

    "무릎이 떨린다."
    "심장은 빨라지는데, 머리는... 멈췄다."
    
    menu:
        "약을 먹는다 (더 이상 두려움을 느끼고 싶지 않다)":
            jump bad_ending_5_1
        "눈을 피한다 (거울에서 시선을 뗀다)":
            jump avoid_eyes
        "거울을 깨뜨린다 (이 지옥을 끝낸다)":
            jump bad_ending_5_2

label avoid_eyes:
    "나는 고개를 돌렸다. 하지만 거울 속의 '나'는 사라지지 않는다."
    "오히려 머릿속으로 파고들어와 나를 조롱한다. 시선을 피할 수 없어!"
    "정신을 차려야 해. 저건 내가 아니야. 저 눈을... 계속해서 피해야만 해."

    # 미니게임: 시선 피하기
    call screen minigame_gaze

    if _return:
        "나는 아직... 괴물이 되고 싶지 않다."
        "가쁜 숨을 몰아쉬며 거울에서 완전히 등을 돌렸다. 등 뒤에서 느껴지는 사악한 시선이 점차 멀어진다."

        $ renpy.sound.play(sfx_clue_gain)
        $ flag_clue_2 = True
        jump floor_4
    else:
        "결국... 거울 속 눈동자에 붙잡히고 말았다."
        m "그래... 이제야 제대로 마주보는구나."
        "시야가 흐려지며, 거울 속 존재와 내가 하나가 되는 끔찍한 감각이 온몸을 덮친다."
        jump bad_ending_5_1