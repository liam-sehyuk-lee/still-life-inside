screen floor_2_comment_screen:
    frame:
        background "#00000080"
        padding (100, 100)
        xalign 0.5
        yalign 0.5
        
        text """oymnipulator314: 나도 그들에게 사육당하고 있어.

restores3421: 여긴 ZONE 12. 누구 있나요?

sometime5134: 함께 저항합시다.""":
            size 50
            color "#ffffff"

label floor_2:
    # 낡은 방. 책상 위에 노트북이 켜져 있고, 화면에는 '피해자'들의 절규가 담긴 게시판이 떠 있다.
    scene bg_2f_mental with dissolve

    n "복도 끝에 불빛이 보여. 문이 열린 방이 있어."
    m "함정일지도 몰라. 놈들이 우리를 끌어들이려는 걸지도. 조심해야 해."
    n "아니. 여기는 뭔가 달라. 책상 위에... 노트북이 켜져 있어."
    "나는 홀린 듯이 화면 앞으로 다가갔다. 화면 가득한 것은 나와 같은 '피해자'들의 절규였다."
    hide window
    show screen floor_2_comment_screen with dissolve
    pause
    hide screen floor_2_comment_screen with dissolve
    
    n "봤어? 드디어 찾았어! 혼자가 아니야! 다른 사람들도 우리와 같은 상황에 있어!"
    m "글쎄... 너무 흥분하지 마. 이건 너무 쉬운 함정 같지 않아?"
    n "무슨 소리야? '나도 사육당하고 있어', '함께 저항합시다'... 봐, 모두 우리와 같아!"
    "하지만 안도감도 잠시, 댓글들을 읽어 내려갈수록 기묘한 위화감이 온몸을 감쌌다."
    "모든 댓글의 문체가... 기묘할 정도로 비슷하다. 마치 한 사람이 쓴 것처럼."
    
    m "익숙한 목소리가 들리는 것 같지 않아?"
    n "맞아... 이상해. 이 글들을 읽고 있는데... 마치 누군가 내 머릿속에서 말을 하는 것 같아."

    "이 섬뜩한 가설을... 확인해야만 한다."
    menu:
        "로그를 정렬한다 (작성자 정보를 확인한다)":
            "나는 떨리는 손으로 로그 정렬 버튼을 눌렀다. 결과가 두려웠지만, 이제는 피할 수 없는 진실과 마주할 때다."
            jump check_logs_2
        "화면을 꺼버린다 (진실을 외면한다)":
            n "아니야... 너무 무서워. 그냥 잊고 싶어."
            m "그래. 잘 생각했어. 때로는 진실을 외면하는 게 더 나은 선택일 때도 있어. 어서 이 방을 벗어나자."
            jump floor_1

label check_logs_2:
    "나는 떨리는 손으로 로그 정렬 버튼을 눌렀다. 그리고 눈을 질끈 감았다. 결과가 두려웠다."
    "눈을 뜨자, 게시글의 작성자 정보가 정렬되어 나타났다."

    # 미니게임: IP 주소 추적
    call screen minigame_ip
    
    if _return:
        "나타난 것은 단 하나의 닉네임. 내가 늘 사용하던... 그 닉네임이었다."
        $ renpy.sound.play(sfx_clue_gain)
        $ flag_clue_5 = True
        
        n "말도 안 돼... 이 모든 글을... 내가 썼다고?"
        m "말도 안 돼! 이건 놈들이 너를 함정에 빠뜨리려는 거야! 속지 마! 이건 놈들이 우리를 조롱하는 시스템이라고!"
        n "하지만... 모든 IP 주소가 똑같아. 이건..."
        m "놈들의 시스템이 우리를 조롱하는 거야! 우리가 찾은 단서들을 파괴하려는 거라고! 어서 이 방을 벗어나자!"
        jump floor_1
    else:
        "손가락이 미끄러졌다. 화면이 깜빡이더니, 모든 로그가 뒤엉키며 아무것도 볼 수 없게 되었다."
        "진실은... 다시 저 너머로 도망쳤다."
        n "아... 젠장... 우리가 중요한 단서를 놓쳤어..."
        m "괜찮아. 다음 기회가 있을 거야. 어서 이 방을 벗어나자."
        jump floor_1