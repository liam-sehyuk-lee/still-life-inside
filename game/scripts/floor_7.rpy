label floor_7:
    # 어둡고 감금된 방. 금속문 너머로 발소리가 들리고, 주인공은 사육당하고 있다고 느낀다.
    scene bg_7f_mental with fade
    # 금속문 너머 발소리 점점 가까워짐. 숨소리, 시계 초침 소리 섞임
    $ renpy.sound.play(sfx_footsteps_creeping, loop=False)
    $ renpy.sound.play(sfx_clock_ticking, loop=True)

    "또 그 시간이다. 째깍이는 시계 소리가 내 심장 소리인지, 진짜 시계 소리인지 이제는 구분조차 가지 않는다."
    "문밖에서 들려오는 저 발소리. 셀 수 없이 많은 날 동안 나를 사육해 온 '그것'의 소리다."
    
    n "오늘도 저 소리야..."
    m "그래. 하지만 우린 괜찮아. 늘 그랬듯이. 이젠 거의 리듬처럼 익숙해진 소리잖아."
    n "하지만... 놈의 발소리가 오늘은 좀 더 무겁게 느껴져."
    m "네가 움직이면, 놈도 반응해. 그러니까 조용히 기다려야 해. 알지?"
    n "응... 알지."
    
    "...그런데 오늘은 느낌이 다르다."
    "문 너머의 그림자가 망설인다. 녀석의 그림자 뒤에 또 다른 그림자가 보인 것만 같다."
    m "봤어? 놈이 주춤했어. 지금이 기회야."
    n "기회라고? 문을 열고 탈출할 기회?"
    "이대로 끝낼 수도 있어. 문이 열리는 그 순간, 놈의 목을 잡을 수 있다면..."
    "나는 이 공포를 끝내고 싶다. 내 몸을 짓누르는 이 감금 상태를."
    n "하지만... 만약 실패하면?"
    m "괜찮아, 함께라면. 우리는 이미 수없이 많은 순간을 같이 헤쳐 왔잖아."

    # 문 아래 그림자가 멈춘다. 식판 그림자가 보임

    menu:
        "조용히 기다린다":
            jump wait_quietly_7
        "문이 열리는 순간을 노린다":
            jump bad_ending_7
            
label wait_quietly_7:
    n "좋아... 숨을 참자. 모든 소리를 죽이고..."
    m "그래, 숨을 참아. 들키면 안 돼. 저것들이 원하는 건 네가 포기하는 거니까."
    n "온몸의 근육이 경직돼. 심장이 목구멍까지 뛰어오르는 것 같아."
    m "진정해. 놈들은 우리가 무너지길 바라고 있어. 우리가 이기려면, 이 소리에 굴복하면 안 돼."

    # 미니게임: 숨소리 참기
    call screen minigame_breath
     
    if _return:
        "다행히... 아무 일도 일어나지 않았다. 소리가 멎는다."
        "문이 천천히 열렸다 닫히고, 식판이 남겨진다."
        "오늘도... 그냥 지나갔다."
        n "휴... 살았다. 우리가 해냈어."
        m "잘했어. 우리에게 또 한 번의 기회가 주어진 거야. 이제 한 층 더 내려가자."
        jump floor_6
    else:
        "실수했다. 너무 긴장한 나머지 침 삼키는 소리가 크게 울려 퍼졌다."
        "문밖의 그림자가... 멈췄다."
        n "안 돼... 들켰어..."
        m "젠장... 결국 우리는 이 방에서 벗어날 수 없나 봐. 끝없이 반복되는군."
        n "우리가... 졌어..."
        "우리는... 결국 다시 같은 자리로 돌아왔다."
        jump bad_ending_7